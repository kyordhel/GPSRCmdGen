; grammar name Category II - People (with incomplete information)
; grammar tier High

$Main      = $task | ($polite $task)


##############################################################################
#
# Tasks
#
##############################################################################
; Greet person matching description
$task  	   = $greet the $person in the $room
$task  	   = $greet the $person $roomI
$task      = $greet {name 1 meta: Provide {name 1}'s description to the robot (e.g. {pron} outfit, tallest, slimmest, oldest, etc.)} in the $room

; Count people
$task  	   = tell me how many $people there are $roomI
$task  	   = tell me how many $peoplege $roomI
$task  	   = tell me how many $peopleR $roomI

; Host
$task  	   = offer something to (eat | drink) to all the $peopleg $roomI
$task  	   = offer something to (eat | drink) to all the $peopleg $roomI
$task  	   = offer something to (eat | drink) to {name 1 meta: Introduce {name 1} to the robot right after giving the command} in the $room

; Bring to person matching description
; $task  	= bring the {object} to the $person in the $room

; Meet and greet a person
$task  	   = $mgreet {void meta:There are two people at {beacon 1}, {name 1} is {gesture 1}  and {name 2} is {gesture 2}}
$task  	   = $mgreetI {void meta:There is only one person at {beacon 1}}

; Follow & guide
$task  	   = $followI | $guideI

; Describe
$task      = $describeI


##############################################################################
#
# Describe
#
##############################################################################
$describeI = tell me what the person $posture in the {beacon?} looks like
$describeI = describe the person $posture in the {beacon?} to me
$describeI = describe the person $posture in the {beacon?} to the person at the {beacon}
$describeI = describe me the person $posture in the {beacon?}



##############################################################################
#
# Follow & guide
#
##############################################################################
; Hard to find a person, easy to follow/guide
$followI   = $vbfollow the $fgwhorI
$guideI    = $vbguide the $fgwhorI to the (exit | {room 2})
$guideI    = $vbguide the $fgwhor {void meta: guide {pron} to the {room 2}}

; Easy-to-find person, but hard to follow/guide
$followI   = $vbfollow the $fgwhoI $fbriefing
$guideI    = $vbguide $fgname from the {beacon 1} to the (exit | {beacon 2}) $gbriefing

; Helpers
$fgwho    = person at the {beacon}
$fgwhor   = $person in the $room
$fgwhoI   = $fgname at the {beacon}
$fgwhorI  = $fgname in the $room
$fgname   = {name 1 meta: Introduce or describe (e.g. {pron} outfit, tallest, slimmest, oldest, etc.) {name 1} right after giving the command}
$fbriefing= {void meta: The Professional Walker must leave the arena and walk through a crowd of at least 5 people who must not let the robot pass. Once the robot has re-encountered its operator, the Professional Walker must lead the robot to {room 2} }
$gbriefing= {void meta: The Professional Walker must, in any order: a) stop following the robot and head towards {beacon 3}, b) stop and wait there, and c) start walking very slow. }
$gbriefing= {void meta: All doors between {beacon 1} and the target location are closed, as well as all paths to get there are blocked. }

##############################################################################
#
# People
#
##############################################################################
$peopleR   = $ppl are $peopleDsc
$peopleDsc = $posture | {gesture}
$peopleDsc = wearing $outfitss
$peopleDsc = wear $color

$people    = $ppl
$people    = $ppl $posture
$peopleg    = $ppl {gesture}
$peoplege    = $ppl are {gesture}
$people    = $ppl wearing $outfitss
$people    = $ppl dressed in $color

$posture   = standing | sitting | lying down
$ppl       = people | children | elders | men | women



##############################################################################
#
# Person
#
##############################################################################
$person    = child
$person    = $lpdesc (man | woman | person)
$person    = (man | woman | person) {gesture}
$person    = (man | woman | person) $rpdesc
$lpdesc    = tallest | smallest | oldest | youngest | slimmest | fattest

$rpdesc    = dressed in an $coloran $outfita
$rpdesc    = dressed in a $colora $outfita
$rpdesc    = wearing ($apparel | a $colora $outfita | an $coloran $outfita | ($colora | $coloran) $outfits)

$colora	   = blue | yellow | black | white | red | gray
$coloran   = orange
$color     = blue | yellow | black | white | red | gray | orange
$outfits   = shoes | pants
$outfita   = (T-shirt) | shirt | blouse | sweater | coat | jacket
$outfitss  = shoes | pants | (T-shirts) | shirts | blouses | sweaters | coats | jackets
$apparel   = a hat | glasses | a necklace | a tie | earrings



##############################################################################
#
# Room
#
##############################################################################
$room      = {room 1 meta: At least three people must be here}
$roomI     = {void meta: At least three people must be at {room 1}}



##############################################################################
#
# Greet
#
##############################################################################
$greet     = $vbgreet
$greet     = ((say hello) | (introduce yourself)) to
$mgreet    = $vbmeet {name} at the {beacon 1}, then (greet | find) {pron} in the $room
$mgreetI   = $vbmeet {name} in the {beacon? 1}, then (greet | find) {pron} in the $room



##############################################################################
#
# Polite
#
##############################################################################
$polite    = Please
$polite    = Could you
$polite    = Robot please
$polite    = Could you please



##############################################################################
#
# Verbs
#
##############################################################################
$vbgreet   = greet | salute | handshake
$vbmeet    = meet | contact | (get to know) | (get acquainted with)
$vbfind    = find | locate | spot | pinpoint | look for
$vbguide   = guide | escort | take | lead | accompany | conduct
$vbfollow  = follow | ((come | go) (behind | after)) | accompany
