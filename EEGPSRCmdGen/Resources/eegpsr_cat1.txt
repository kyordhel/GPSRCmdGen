; grammar name Category I - Three at once
; grammar tier Moderate

$Main     = $complex.
$Main     = $polite $complex.


##############################################################################
#
# Complex commands
#
##############################################################################
$complex  = $triple
$complex  = $single, $single, and ($single | $singleSp)
$complex  = $doublel, and ($single | $singleSp)
$complex  = $single, $doubler

##############################################################################
#
# Triple task commands
#
##############################################################################
$triple   = $findord, and ($place | $deliver)
$triple   = $findpd, and ($guide | $follow)
$triple   = $findprd, and ($guide | $follow)

##############################################################################
#
# Double task commands
#
##############################################################################
; go get it
$doubler  = $take, and ($place | $deliver)
$doublel  = $take, ($place | $deliver)

;find person or object
$doublel  = $findord
$doublel  = $findprd

; find and follow
$doubler  = $findps, and $follow
$doubler  = $findprs, and $follow
$doublel  = $findps, $follow
$doublel  = $findprs, $follow

; find and guide
$doubler  = $findps, and $guide
$doubler  = $findprs, and $guide
$doublel  = $findps, $guide
$doublel  = $findprs, $guide

##############################################################################
#
# Single task commands
#
##############################################################################
$single   = $findps
$single   = $findprs
$single   = $findors
$single   = $take
$single   = $mvobj
$singleSp = $vbspeak $whattosay
$singleSp = answer a {question}

##############################################################################
#
# Tasks
#
##############################################################################
; Find object in room
$findord  = $vbgor the {room}, $vbfind the {object}
$findors  = $vbfind the {object} in the {room}

; Take an object
$take     = $vbtake the {aobject} from the {placement}

; Place an object
$place    = $vbplace it on the {placement}

;move object to target
$mvobj    = $vbplace {object} on the {placement}

; Deliver an object
$deliver  = $vbdeliver it to (me | {name} at the {beacon})

; Find people in location
$findpd   = $vbgopl to the {beacon}, $vbfind {name}
$findps   = $vbfind {name} at the {beacon}

; Find people in room
$findprd  = $vbgor the {room}, $vbfind {name}
$findprs  = $vbfind {name} in the {room}

; Follow people
$follow   = $vbfollow {pron}

; Guide people
$guide    = $vbguide {pron} to the ( {room} | {beacon} | exit )


##############################################################################
#
# What to say
#
##############################################################################
$whattosay = something about yourself
$whattosay = the time
$whattosay = what day is (today | tomorrow)
$whattosay = your team's (name | country | affiliation)
$whattosay = the day of the (week | month)
$whattosay = whether you dream or not on electric sheep
$whattosay = a joke

##############################################################################
#
# Verbs
#
##############################################################################
$vbtake    = get | grasp | take | retrieve | pick up
$vbplace   = put | place | leave | set
$vbspeak   = tell | say
$vbgopl    = go to | navigate to
$vbgor     = $vbgopl | enter
$vbfind    = find | locate | spot | pinpoint | look for
$vbguide   = guide | escort | take | lead | accompany | conduct
$vbfollow  = follow | ((come | go) (behind | after)) | accompany
$vbdeliver = bring | deliver | give | hand | hand over

##############################################################################
#
# Polite
#
##############################################################################
$polite    = Please
$polite    = Could you
$polite    = Robot please
$polite    = Could you please

