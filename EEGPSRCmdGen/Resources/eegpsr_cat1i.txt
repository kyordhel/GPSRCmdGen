; grammar name Category I - Three at once (with incomplete information)
; grammar tier Moderate

$Main     = $complex.
$Main     = $polite $complex.


##############################################################################
#
# Complex commands
#
##############################################################################
$complex  = $triple
$complex  = $singleI, $single, and ($single | $singleSp)
$complex  = $single, $singleI, and ($single | $singleSp)
$complex  = $single, $single, and ($singleI | $singleSpI)
$complex  = $doublelI, and ($single | $singleSp)
$complex  = $doublel, and ($singleI | $singleSpI)
$complex  = $singleI, $doubler
$complex  = $single, $doublerI

##############################################################################
#
# Triple task commands
#
##############################################################################
$triple   = $findordI, and ($place | $deliver)
$triple   = $findord, and ($placeI | $deliverI)
$triple   = $findpdI, and $guide
$triple   = $findpd, and $guideI
$triple   = $findprdI, and ($guide | $follow)
$triple   = $findprd, and $guideI

##############################################################################
#
# Double task commands
#
##############################################################################
; go get it
$doubler  = $take, and ($place | $deliver)
$doublel  = $take, ($place | $deliver)
$doublerI = $take, and ($placeI | $deliverI)
$doublerI = $takeI, and ($place | $deliver)
$doublelI = $take, ($placeI | $deliverI)
$doublelI = $takeI, ($place | $deliver)

;find person or object
$doublel  = $findord
$doublel  = $findprd
$doublelI = $findordI
$doublelI = $findprdI

; find and follow
$doubler  = $findprs, and $follow
$doublel  = $findprs, $follow
$doublerI = $findprsI, and $follow
$doublelI = $findprsI, $follow

; find and guide
$doubler  = $findps, and $guide
$doublel  = $findps, $guide
$doublerI = $findps, and $guideI
$doublerI = $findpsI, and $guide
$doublelI = $findps, $guideI
$doublelI = $findpsI, $guide

##############################################################################
#
# Single task commands
#
##############################################################################
$single   = $findps
$single   = $findprs
$single   = $findors
$single   = $take
$single   = $mvobj
$singleSp = $vbspeak $whattosay
$singleSp = answer a {question}

$singleI  = $findpsI
$singleI  = $findprsI
$singleI  = $findorsI
$singleI  = $takeI
$singleI  = $mvobjI
$singleSpI= (speak | (say something)) {void meta: When asked, reply to the robot: "$whattosay" }

##############################################################################
#
# Tasks
#
##############################################################################
; Find object in room
$findord  = $vbgor to the {room}, $vbfind the {object}
$findors  = $vbfind a {object} in the {room}
$findordI = $vbgor to the {room}, $vbfind the {object?}
$findorsI = $vbfind (a | the) {object meta: Robot must place it on the {placement}}

; Take an object
$take     = $vbtake the {aobject} from the {placement}
$takeI    = $vbtake the {aobject? meta: Place to } from the {placement}
$takeI    = $vbtake the {aobject meta: Robot will find it on the {placement}}

; Place an object
$place    = $vbplace it on the {placement}
$placeI   = $vbplace it to the {placement?}

;move object to target
$mvobj    = $vbplace {object} on the {placement}
$mvobjI   = $vbplace the {object} in the {placement?}
$mvobjI   = $vbplace a {object? meta: place 3 objects of the same category at {placement 1}} on the {placement 1}

; Deliver an object
$deliver  = $vbdeliver it to (me | {name} at the {beacon})
$deliverI = $vbdeliver it to {name meta: {pron sub} is (sitting | standing | lying | waving ) at the {beacon}}

; Find people in location
$findpd   = $vbgopl to the {beacon}, $vbfind {name}
$findps   = $vbfind {name} at the {beacon}
$findpdI  = $vbgopl to the {beacon}, $vbfind {name meta: {pron sub} is (sitting | standing | lying | waving ). Place three people there}
$findpsI  = $vbfind {name meta: {pron sub} is (sitting | standing | lying | waving ) at the {beacon}}

; Find people in room
$findprd  = $vbgor to the {room}, $vbfind {name}
$findprs  = $vbfind {name} in the {room}
$findprdI = $vbgor to the {room}, $vbfind {name meta: {pron sub} is (sitting | standing | waving ). Place three people in the room}
$findprsI = $vbfind {name meta: {pron sub} is (sitting | standing | waving ) at the {room}}

; Follow people
$follow   = $vbfollow {pron}

; Guide people
$guide    = $vbguide {pron} to the ( {room} | {beacon} | exit )
$guideI   = escort {pron} {void meta: Escort {pron} to the ( {room} | {beacon} | exit )}


##############################################################################
#
# What to say
#
##############################################################################
$whattosay = something about yourself
$whattosay = the time
$whattosay = what day is (today | tomorrow)
$whattosay = your team's (name | country | affiliation)
$whattosay = the day of the (week | month)
$whattosay = whether you dream or not on electric sheep
$whattosay = a joke


##############################################################################
#
# Verbs
#
##############################################################################
$vbtake    = get | grasp | take | retrieve | pick up
$vbplace   = put | place | leave | set
$vbspeak   = tell | say
$vbgopl    = go | navigate
$vbgor     = $vbgopl | enter
$vbfind    = find | locate | spot | pinpoint | look for
$vbguide   = guide | escort | take | lead | accompany | conduct
$vbfollow  = follow | ((come | go) (behind | after)) | accompany
$vbdeliver = bring | deliver | give | hand | hand over

##############################################################################
#
# Polite
#
##############################################################################
$polite    = {void} | Please
$polite    = Could you
$polite    = Robot please
$polite    = Could you please

